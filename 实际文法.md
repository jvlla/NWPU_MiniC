// 函数块外的文法
Program         → Segment
                | Program Segment
Segment         → Type Def
Type            → INT
                | VOID
Def             → '*' IDENT Deflist
                | IDENT Idtail
Deflist         → ',' Defdata Deflist
                | ';'
Defdata         → IDENT Vardef
                | '*' IDENT
Vardef          → ε
                | '[' NUM ']' Vardef
Idtail          → Vardef Deflist
                | '(' Para ')' Functail
Functail        → Blockstat
                | ';'
Para            → ε
                | Onepara Oneparas
Oneparas        → ε
                | ',' Onepara Oneparas
Onepara         → Type Paradata
Paradata        → '*' IDENT 
                | IDENT Paradatatail
Paradatatail    → '[' ']' Paradatatails
                | Paradatatails
Paradatatails   → ε
                | '[' NUM ']' Paradatatails
// 以下是函数块内文法
// 先开始是一些语句stmt吧
Subprogram      → ε 
                | Subprogram Onestatement
Onestatement    → Localdef 
                | Statement
Localdef        → Type Defdata Deflist
Statement       → Whilestat
                | Ifstat
                | Breakstat
                | Continuestat
                | Returnstat
                | Blockstat
                | Assignstat
                | Emptystat
Breakstat       → 'break' ';'
Continuestat    → 'continue' ';'
Returnstat      → 'return' Altexpr ';'
Assignstat      → Altexpr ';'
Blockstat       → '{' Subprogram '}'
Emptystat       → ';'
// 复杂一些的stmt
Whilestat       → 'while' '(' Expr ')' Statement
Ifstat          → 'if'  '(' Expr ')' Statement Elsestat
Elsestat        → ε
                | 'else' Statement
// 以下是表达式expr
Altexpr         → ε
                | Expr
Expr            → Assexpr
Assexpr         → Orexpr Asstail
Asstail         → ε
                | '=' Assexpr Asstail
// 与或部分
Orexpr          → Andexpr Ortail
Ortail          → ε
                | '||' Andexpr Ortail
Andexpr         → Cmpexpr Andtail
Andtail         → ε
                | '&&' Cmpexpr Andtail
Cmpexpr         → Aloexpr Cmptail
Cmptail         → ε
                | Cmps Aloexpr Cmptail
Cmps            → GE | '>' | '<' | LE | EQ | NE
Aloexpr         → Item Alotail
Alotail         → ε
                | Addsub Item Alotail
Addsub          → '+' | '-'
Item            → Factor Itemtail
Itemtail        → ε
                | Muldiv Factor Itemtail
Muldiv          → '*' | '/'
Factor          → Lop Factor
                | Val
Lop             → '!' | '-' | '&' | '*' | INCR | DECR
Val             → Elem
                | Elem Rop
Rop             → INCR | DECR
Elem            → IDENT Idexpr
                | '(' Expr')'
                | Literal
Idexpr          → ε
                | '[' Expr ']' 
                | '(' Realarg ')'
Literal         → NUM
Realarg         → ε
                | Arg Realargs
Realargs        → ε
                | ',' Arg Realargs
Arg             → Expr